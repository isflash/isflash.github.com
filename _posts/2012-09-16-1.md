---
layout: master
title: RPG制作之 -- 人物动作渲染

---

动作渲染有两种方式，一种是flash的movieclip方式，一种是块传输技术（spritesheet），这两种方式各有利弊。使用movieclip方式优点是：制作方便，缺点是：由于所有动作都基于movieclip类渲染，这样会浪费内存和cpu资源。而spritesheet的优缺点和movieclip正好相反。为了让用户的游戏体验更好，flash在制作大型游戏，一般都选用spritesheet方式。

<object type="application/x-shockwave-flash" width="105" height="120">
 <param name="movie" value="../../../images/posts/sheet.swf"/>
 <param name="bgcolor" value="#ffffff"/>
 <embed src="../../../images/posts/sheet.swf"
        type="application/x-shockwave-flash"
        bgcolor="#ffffff"
        width="105"
        height="120">
 </embed>
</object>

[源码](https://gist.github.com/41500281261d4bcb6d12)


	package com.zs.utils
	{
	import flash.display.Bitmap;
	import flash.display.BitmapData;
	import flash.display.Loader;
	import flash.display.LoaderInfo;
	import flash.events.Event;
	import flash.events.IOErrorEvent;
	import flash.geom.Point;
	import flash.geom.Rectangle;
	import flash.net.URLRequest;
	
	public class SheetMoive extends Bitmap
	{
		private var sheets:Object = {};
		private var action:String;
		private var actionW:Number = 105;
		private var actionH:Number = 120;
		
		public function SheetMoive(w:Number=105,h:Number=120)
		{
			this.bitmapData = new BitmapData(actionW,actionH);
		}
		
		public function addSheet(url:String,action:String):void
		{
			var loader:Loader = new Loader();
			loader.contentLoaderInfo.addEventListener(Event.COMPLETE,onLoaded);
			loader.contentLoaderInfo.addEventListener(IOErrorEvent.IO_ERROR,onError);
			loader.name = action;
			loader.load(new URLRequest(url));
		}
		
		public function go(action:String):Boolean
		{
			if(sheets[action]){
				this.action = action;
				this.addEventListener(Event.ENTER_FRAME,render);
				return true;
			}
			return false;
		}
		
		private var sheetIndex:int = 0;
		private function render(e:Event):void
		{
			if(sheetIndex >= sheets[action].sheetNum){
				sheetIndex = 0;
			}
			this.bitmapData.lock();
			this.bitmapData.copyPixels(sheets[action].pic,new Rectangle(sheetIndex * actionW,0,actionW,actionH),new Point(0,0));
			this.bitmapData.unlock();
			sheetIndex++;
		}
		
		private function onError(e:IOErrorEvent):void
		{
			
		}
		
		private function onLoaded(e:Event):void
		{
			var loader:Loader = LoaderInfo(e.target).loader;
			var bitmapdata:BitmapData = (loader.content as Bitmap).bitmapData;
			sheets[loader.name] = {pic:bitmapdata,sheetNum:Math.floor(bitmapdata.width/105)};
			this.dispatchEvent(new Event('done'));
		}
	}
	}
	
引用：	
	
	package
	{
	import com.zs.utils.SheetMoive;
	
	import flash.display.LoaderInfo;
	import flash.display.Sprite;
	import flash.events.Event;
	import flash.events.MouseEvent;

	[SWF(width='105',height='120',backgroundColor='0xcccccc')]
	public class Main extends Sprite
	{
		private var ren:SheetMoive = new SheetMoive();
		
		public function Main()
		{
			this.addEventListener(Event.ADDED_TO_STAGE,init);
		}
		
		private function init(e:Event):void
		{
			this.stage.align = 'TL';
			this.stage.scaleMode = 'noScale';
			this.stage.addEventListener(MouseEvent.CLICK,changeAction);
			this.addChild(ren);
			ren.addSheet('/assets/character/walk.png','walk');
			ren.addSheet('/assets/character/stand.png','stand');
			ren.addEventListener('done',onStart);
		}
		
		private var change:Boolean;
		protected function changeAction(event:MouseEvent):void
		{
			if(change){
				ren.go('stand');
			}else{
				ren.go('walk');
			}
			change = !change;
		}
		
		private function onStart(e:Event):void
		{
			ren.go('walk');
		}
	}
	}