---
layout: master
title: RPG制作之--大地图渲染

---

大地图的渲染是在小的缩略图渲染之后进行的，于缩略图渲染方式不同的是，大地图渲染需要分块加载，分块渲染。

<object type="application/x-shockwave-flash" width="600" height="400">
 <param name="movie" value="../../../images/posts/bigMap.swf"/>
 <param name="bgcolor" value="#ffffff"/>
 <embed src="../../../images/posts/bigMap.swf"
        type="application/x-shockwave-flash"
        bgcolor="#ffffff"
        width="600"
        height="400">
 </embed>
</object>

	/**
 	* 整个拼合的大图尺寸：3200 * 720
 	* 这个地图缩略图尺寸：190 * 42
	* 每个tile尺寸是：320 * 360
 	* 渲染尺寸：1320 * 720,由于该游戏地图是横版的，所以只在x方向多渲染了一个tile
 	*/
	package com.zs
	{
	import com.zs.utils.MapLoader;
	
	import flash.display.Bitmap;
	import flash.display.BitmapData;
	import flash.display.Loader;
	import flash.display.LoaderInfo;
	import flash.display.Shape;
	import flash.display.Sprite;
	import flash.events.Event;
	import flash.geom.Matrix;
	import flash.geom.Point;
	import flash.geom.Rectangle;
	import flash.net.URLRequest;
	import flash.utils.setInterval;
	
	public class BigMapFill extends Sprite
	{
		private var _smallMap:BitmapData;
		/**
		 * 地图缓冲区（源地图）
		 */
		private var buffer:BitmapData;
		
		private var shape:Bitmap = new Bitmap();
		
		private var smallMapFilled:Boolean;
		
		public function BigMapFill()
		{
			buffer=new BitmapData(1320,720,false);
			
			shape.bitmapData = buffer;
			this.addChild(shape);
		}
		
		public function fillSmallMap():Boolean
		{
			if(smallMapFilled == false){
				loadSmallMap();
			}
			return smallMapFilled;
		}
		
		public function fillBigMap():void
		{
			var i:int = 0;
			var j:int = 0;
			setInterval(function():void{
				if(i>=4){
					i=0;
					j=1;
				}
				var loader:MapLoader = new MapLoader();
				loader.load(getSwfCatalog()+'/assets/tiles/301/'+j+'_'+i+'.jpg');
				loader.addEventListener(Event.COMPLETE,onTileLoaded);
				loader.data = {i:i,j:j};
				i ++;
			},1000);
			
			/*for(i=0;i<4;i++){
				for(j=0;j<2;j++){
					var loader:MapLoader = new MapLoader();
					loader.load('assets/tiles/301/'+j+'_'+i+'.jpg');
					loader.addEventListener(Event.COMPLETE,onTileLoaded);
					loader.data = {i:i,j:j};
				}
			}*/
		}
		
		public function getSwfCatalog():String
		{
			//通用代码段，获取播放器自身所在的路径 + 补全URL路径字符串以支持相对路径
			var playerrealurl:String = "";
			var playervarsstr:String = "";
			var playerrootpath:String = "";
			loaderInfo.url.indexOf("?", 0)>=0 ? playerrealurl=loaderInfo.url.slice(0, loaderInfo.url.indexOf("?", 0)) : playerrealurl=loaderInfo.url;
			loaderInfo.url.indexOf("?", 0)>=0 ? playervarsstr=loaderInfo.url.slice(loaderInfo.url.indexOf("?", 0)+1) : playervarsstr="";
			playerrealurl.lastIndexOf("/", playerrealurl.length)>=0 ? playerrootpath=playerrealurl.slice(0, playerrealurl.lastIndexOf("/", playerrealurl.length)) : playerrootpath="";
			
			return playerrootpath;
		}
		
		private function onTileLoaded(e:Event):void
		{
			var rect:Rectangle = new Rectangle(0,0,320,360);
			var point:Point = new Point(MapLoader(e.target).data.i*320,MapLoader(e.target).data.j * 360);
			buffer.lock();
			buffer.copyPixels(Bitmap(MapLoader(e.target).content).bitmapData,rect,point);
			buffer.unlock();
		}
		
		private function loadSmallMap():void
		{
			var loader:Loader = new Loader();
			loader.contentLoaderInfo.addEventListener(Event.COMPLETE,smallMapLoaded);
			loader.load(new URLRequest('assets/s.jpg'));
		}
		
		private function smallMapLoaded(e:Event):void
		{
			var loadinfo:LoaderInfo = e.target as LoaderInfo;
			
			_smallMap = (loadinfo.content as Bitmap).bitmapData;
			
			var per:Number = 3200/_smallMap.width;
			buffer.draw(_smallMap,new Matrix(per,0,0,per),null,null,null,true);
			
			smallMapFilled = true;
		}
	}
	}
	
地图加载器：

	package com.zs.utils
	{
	import flash.display.DisplayObject;
	import flash.display.Loader;
	import flash.display.LoaderInfo;
	import flash.events.Event;
	import flash.events.EventDispatcher;
	import flash.events.IOErrorEvent;
	import flash.net.URLRequest;
	
	[Event(name='COMPLETE',type='flash.events.Event')]
	public class MapLoader extends EventDispatcher
	{
		public var data:Object = {};
		
		private var _content:DisplayObject;
		
		public function MapLoader()
		{
		}
		
		public function load(url:String):MapLoader
		{
			var loader:Loader = new Loader();
			loader.contentLoaderInfo.addEventListener(Event.COMPLETE,onLoaded);
			loader.contentLoaderInfo.addEventListener(IOErrorEvent.IO_ERROR,onError);
			loader.load(new URLRequest(url));
			
			return this;
		}
		
		private function onError(e:IOErrorEvent):void
		{
			LoaderInfo(e.target).loader.removeEventListener(Event.COMPLETE,onLoaded);
			LoaderInfo(e.target).loader.removeEventListener(IOErrorEvent.IO_ERROR,onError);
		}
		
		public function get content():DisplayObject
		{
			return _content;
		}
		
		private function onLoaded(e:Event):void
		{
			LoaderInfo(e.target).loader.removeEventListener(Event.COMPLETE,onLoaded);
			LoaderInfo(e.target).loader.removeEventListener(IOErrorEvent.IO_ERROR,onError);
			
			_content = LoaderInfo(e.target).content;
			
			this.dispatchEvent(new Event(Event.COMPLETE));
		}
	}
	}

使用：

	package
	{
	import com.zs.BigMapFill;
	
	import flash.display.Sprite;
	import flash.events.Event;
	import flash.events.MouseEvent;

	[SWF(width='600',height='400')]
	public class Main extends Sprite
	{
		private var map:BigMapFill = new BigMapFill();
		
		public function Main()
		{
			this.addEventListener(Event.ADDED_TO_STAGE,init);
		}
		
		private function init(e:Event):void
		{
			this.stage.align = 'TL';
			this.stage.scaleMode = 'noScale';
			this.stage.addEventListener(MouseEvent.CLICK,onStart);
			this.addChild(map);
		}
		
		private function onStart(event:MouseEvent):void
		{
			if(map.fillSmallMap()){
				map.fillBigMap();
			}
		}
	}
	}